cmake_minimum_required(VERSION 3.20)
project(amd-llvm-flang C CXX ASM)

# Source directory for the LLVM monorepo
set(LLVM_MONOREPO_DIR "${CMAKE_CURRENT_SOURCE_DIR}/../amd-llvm")

# Common cmake utils path that sub-projects expect
set(LLVM_COMMON_CMAKE_UTILS "${LLVM_MONOREPO_DIR}/cmake")

#=============================================================================
# Find pre-built LLVM/Clang to get all the variables
#=============================================================================
find_package(LLVM REQUIRED CONFIG)
find_package(Clang REQUIRED CONFIG)

message(STATUS "Found LLVM ${LLVM_PACKAGE_VERSION}")
message(STATUS "Using LLVMConfig.cmake in: ${LLVM_DIR}")
message(STATUS "Using ClangConfig.cmake in: ${Clang_DIR}")

#=============================================================================
# Set up LLVM cmake module paths
#=============================================================================
list(APPEND CMAKE_MODULE_PATH ${LLVM_CMAKE_DIR})
list(APPEND CMAKE_MODULE_PATH ${CLANG_CMAKE_DIR})
list(APPEND CMAKE_MODULE_PATH "${LLVM_COMMON_CMAKE_UTILS}/Modules")

#=============================================================================
# Include LLVM/Clang cmake utilities
#=============================================================================
include(HandleLLVMOptions)
include(AddLLVM)
include(TableGen)
include(CMakePushCheckState)
include(AddClang)

#=============================================================================
# Set up variables that sub-projects expect from an LLVM tree build
# These would normally be set by llvm/CMakeLists.txt
#=============================================================================

# Source directories
set(LLVM_MAIN_SRC_DIR "${LLVM_MONOREPO_DIR}/llvm")
set(LLVM_SOURCE_DIR "${LLVM_MONOREPO_DIR}/llvm")
set(CLANG_SOURCE_DIR "${LLVM_MONOREPO_DIR}/clang")

# Binary/build directories - point to installed LLVM for tools, local for build outputs
set(LLVM_BINARY_DIR "${CMAKE_BINARY_DIR}")
set(LLVM_TOOLS_BINARY_DIR "${LLVM_BINARY_DIR}/bin")

# Output directories
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib${LLVM_LIBDIR_SUFFIX}")
set(LLVM_RUNTIME_OUTPUT_INTDIR "${CMAKE_BINARY_DIR}/${CMAKE_CFG_INTDIR}/bin")
set(LLVM_LIBRARY_OUTPUT_INTDIR "${CMAKE_BINARY_DIR}/${CMAKE_CFG_INTDIR}/lib${LLVM_LIBDIR_SUFFIX}")

# Version variables
set(CLANG_VERSION_MAJOR ${LLVM_VERSION_MAJOR})
set(CLANG_VERSION_MINOR ${LLVM_VERSION_MINOR})
set(CLANG_VERSION_PATCH ${LLVM_VERSION_PATCH})
set(PACKAGE_VERSION ${LLVM_PACKAGE_VERSION})

# Include directories - both installed LLVM and source trees
include_directories(${LLVM_INCLUDE_DIRS})
include_directories(${CLANG_INCLUDE_DIRS})

# LLVM sub-project targets don't properly advertise their include directories,
# so we manually add source and build include dirs for transitive dependencies.
# Source includes (for .h files)
include_directories(${LLVM_MONOREPO_DIR}/mlir/include)
include_directories(${LLVM_MONOREPO_DIR}/clang/include)
# Build includes (for generated .h.inc files from tablegen)
include_directories(${CMAKE_BINARY_DIR}/tools/mlir/include)
include_directories(${CMAKE_BINARY_DIR}/tools/clang/include)

# Disable tests - they depend on LLVM-internal targets
set(LLVM_INCLUDE_TESTS OFF)
set(LLVM_BUILD_TESTS OFF)
set(MLIR_INCLUDE_TESTS OFF)
set(FLANG_INCLUDE_TESTS OFF)

# Other LLVM settings sub-projects check
set(LLVM_ENABLE_PROJECTS "")
set(LLVM_ENABLE_RUNTIMES "")


#=============================================================================
# Flang settings
# Note: FLANG_RUNTIME_F128_MATH_LIB disabled - requires flang-rt which we don't build here
#=============================================================================
set(FLANG_RUNTIME_F128_MATH_LIB "libquadmath")

#=============================================================================
# Generate quadmath_wrapper.h
# This is a bug in upstream LLVM: the wrapper is only generated by flang-rt but
# the flang compiler itself needs it when HAS_QUADMATHLIB is detected. We include
# FlangCommon.cmake to get the same detection logic, then generate the wrapper
# into a directory we add to the include path.
#=============================================================================
include(${LLVM_MONOREPO_DIR}/flang/cmake/modules/FlangCommon.cmake)

if (FLANG_INCLUDE_QUADMATH_H)
  set(QUADMATH_WRAPPER_DIR "${CMAKE_BINARY_DIR}/include")
  configure_file(
    "${LLVM_MONOREPO_DIR}/flang/cmake/quadmath_wrapper.h.in"
    "${QUADMATH_WRAPPER_DIR}/quadmath_wrapper.h"
  )
  include_directories(${QUADMATH_WRAPPER_DIR})
  message(STATUS "Generated quadmath_wrapper.h in ${QUADMATH_WRAPPER_DIR}")
endif()


#=============================================================================
# Force non-standalone mode for all sub-projects
# They will use in-tree code paths expecting targets to be available
#=============================================================================
set(MLIR_STANDALONE_BUILD OFF)
set(FLANG_STANDALONE_BUILD OFF)
set(OPENMP_STANDALONE_BUILD OFF)

#=============================================================================
# MLIR (build dependency for Flang)
# Put MLIR at tools/mlir to match normal LLVM tree layout
#=============================================================================
set(MLIR_MAIN_SRC_DIR "${LLVM_MONOREPO_DIR}/mlir")
set(MLIR_SOURCE_DIR "${LLVM_MONOREPO_DIR}/mlir")
set(MLIR_BINARY_DIR "${CMAKE_BINARY_DIR}/tools/mlir")

# Disable MLIR dylib linking - we build MLIR as static libraries here, not as
# a shared library. MLIR_LINK_MLIR_DYLIB defaults to LLVM_LINK_LLVM_DYLIB which
# is ON for our pre-built LLVM, but we're building MLIR ourselves.
set(MLIR_LINK_MLIR_DYLIB OFF)

# Note: Cannot use EXCLUDE_FROM_ALL because Flang depends on MLIR tablegen
# targets that wouldn't be built otherwise (dependency chains not fully expressed)
add_subdirectory(${LLVM_MONOREPO_DIR}/mlir tools/mlir EXCLUDE_FROM_ALL)

#=============================================================================
# Flang
#=============================================================================
add_subdirectory(${LLVM_MONOREPO_DIR}/flang flang)


# #=============================================================================
# # OpenMP runtime
# #=============================================================================
# add_subdirectory(${LLVM_MONOREPO_DIR}/openmp openmp)

# #=============================================================================
# # Offload runtime (GPU offloading support)
# #=============================================================================
# add_subdirectory(${LLVM_MONOREPO_DIR}/offload offload)

#=============================================================================
# Dependency hacks
# MLIR tablegen targets don't propagate dependencies properly across add_subdirectory
# boundaries. We need to explicitly add dependencies on tablegen outputs that
# Flang headers include.
#=============================================================================

add_dependencies(FortranSupport MLIRBuiltinTypeInterfacesIncGen)
add_dependencies(FIRSupport MLIRLinalgOpsIncGen)

# VCSVersion.inc is generated in the Support binary dir but isn't in include path
target_include_directories(FortranSupport PRIVATE
  ${CMAKE_BINARY_DIR}/flang/lib/Support)
